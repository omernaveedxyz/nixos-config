{ config, pkgs, ... }:
{
  # Wireguard interfaces
  networking.wg-quick.interfaces.wg0 = {
    # Whether to bring up this interface automatically during boot
    autostart = true;

    # The IP addresses of the interface
    address = [ "10.2.0.2/32" ];

    # 16-bit port for listening
    listenPort = 51820;

    # Private key file as generated by wg genkey
    privateKeyFile = config.sops.secrets."networking/wg-quick/interfaces/wg0/privateKeyFile".path;

    # The IP addresses of DNS servers to configure
    dns = [ "10.2.0.1" ];

    # Commands called after the interface setup
    postUp = ''
      # Mark packets on the wg0 interface
      wg set wg0 fwmark 51820

      # Forbid anything else which doesn't go through wireguard VPN on
      # ipV4 and ipV6
      ${pkgs.iptables}/bin/iptables -A OUTPUT \
        ! -d 10.0.2.0/24 \
        ! -o wg0 \
        -m mark ! --mark $(wg show wg0 fwmark) \
        -m addrtype ! --dst-type LOCAL \
        -j REJECT
      ${pkgs.iptables}/bin/ip6tables -A OUTPUT \
        ! -o wg0 \
        -m mark ! --mark $(wg show wg0 fwmark) \
        -m addrtype ! --dst-type LOCAL \
        -j REJECT
    '';

    # Command called before the interface is taken down
    preDown = ''
      ${pkgs.iptables}/bin/iptables -D OUTPUT \
        ! -o wg0 \
        -m mark ! --mark $(wg show wg0 fwmark) \
        -m addrtype ! --dst-type LOCAL \
        -j REJECT
      ${pkgs.iptables}/bin/ip6tables -D OUTPUT \
        ! -o wg0 -m mark \
        ! --mark $(wg show wg0 fwmark) \
        -m addrtype ! --dst-type LOCAL \
        -j REJECT
    '';

    # Peers linked to the interface
    peers = [
      {
        # The base64 public key to the peer
        publicKey = "gU9CLkRxLUarj9+MtswvE/2Tvclx32w5aoSYeY3eEX8=";

        # List of IP (v4 or v6) addresses with CIDR masks from which this peer is allowed to send incoming traffic and to which outgoing traffic for this peer is directed
        allowedIPs = [ "0.0.0.0/0" ];

        # Endpoint IP or hostname of the peer, followed by a colon, and then a port number of the peer
        endpoint = "163.5.171.2:51820";
      }
    ];
  };

  # Specify encrypted sops secret to access
  sops.secrets."networking/wg-quick/interfaces/wg0/privateKeyFile" = {
    sopsFile = ../../../secrets.yaml;
  };
}
